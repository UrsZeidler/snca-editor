--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: common
Element-Type: context
Element-Version: 2.0
Id: _oxuLcEEZEemohouIo4TVTw
Runtime-Version: 2.3.0.201806262310
Save-Time: 3/22/19 8:52 AM

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

Set the basic values and provides basic functions.
------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

global [val basedir [get-java-property basepath]] [val datadir [get-java-property "test-data"]] [val imagedir
	[get-java-property images]]

proc "import-snca" [val viewname] [val target] [val pathtosnca] {
	get-view $viewname | get-tree | select $target | get-menu -path "Import..." | click
	get-window Import | get-button "Next >" | click
	set-dialog-result File $pathtosnca
	with [get-window "File Import Wizard"] {
		get-button "Browse..." | click
		screen-shot-window "File Import Wizard" "new-project2.png"

		get-button Finish | click
	}
}
proc "create-project" [val projectname] {
	get-button New | click -arrow
	get-menu -path "Project..." | click
	with [get-window "New Project"] {
		get-tree | select "General/Project"
		get-button "Next >" | click
		get-editbox -after [get-label "Project name:"] | set-text $projectname
		get-button Finish | click
	}
}

proc createLinkedFolder [val container] [val externalpath] [val foldername] {
	get-view Navigator | get-tree | select $container | get-menu -path "New/Other..." | click
	with [get-window New] {
		get-tree | select "General/Folder"
		get-button "Next >" | click
	}
	with [get-window "New Folder"] {
		get-button "Advanced >>" | click
		get-button "Link to alternate location (Linked Folder)" | click
		get-editbox -after [get-label "Folder name:"] | set-text $foldername
	}
	set-dialog-result Folder $externalpath
	with [get-window "New Folder"] {
		get-button "Browse..." | click
		get-button Finish | click
	}
}

proc "copy-resource" [val from] [val to] {
	with [get-view Navigator | get-tree] {
		select $from | get-menu -path Copy | click
		select $to | get-menu -path Paste | click
	}

	try -times 1 -delay 1000 -command {
		get-window "Resource Exists" | get-button "Yes To All" | click
	} -catch {
		log "resource exist"
	}
}

/* Take a screenshot of the window. */
proc "screen-shot-eclispe-window" [val name] {
	get-eclipse-window | get-object | save-screenshot -out [concat $imagedir [get-java-property "file.separator"] $name]
}

proc "screen-shot-window" [val window] [val name] {
	get-window $window | get-object | save-screenshot -out [concat $imagedir [get-java-property "file.separator"] $name]
}

proc "screen-shot-view" [val view] [val name] {
	get-view $view | get-tree | get-object | invoke getParent | invoke getParent | invoke getParent | save-screenshot
		-out [concat $imagedir "/" $name]
}

proc "get-last" [val path] {
	split -str $path -sep "/" -trimResults -omitEmptyStrings | to-list | get [minus [split -str $path -sep "/"
		-trimResults -omitEmptyStrings | length] 1]
}

proc "get-first" [val path] {
	split -str $path -sep "/" -trimResults -omitEmptyStrings | to-list | get 0
}
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
